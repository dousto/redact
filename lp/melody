% File: melody
% Author: Doug Stoeckmann (dousto@gmail.com)
%
% To be run in conjunction with an answer set produced by the progression logic file,
% as well as an answer set produced by the rhythm logic file.

melodyNoteRange(48..72).
time(from..to).

% At each timestep either play or dontplay a note %
1{play(T);dontplay(T)}1 :- time(T).
% Make sure to play a note from the scale
1{playNote(MNRL, T) : melodyNoteRange(MNRL), scaleNote(MNRL)}1 :- play(T), time(T).

% Hold a note when it gets played
holdNote(MNR, T) :- playNote(MNR, T).
% Continue to hold it if no other note is played
holdNote(MNR, T) :- dontplay(T), holdNote(MNR, T-1), melodyNoteRange(MNR), time(T).
% Release a note when another gets played
releaseNote(MNR, T) :- play(T), holdNote(MNR, T-1), melodyNoteRange(MNR), time(T).

% If coming from a previous note, limit the note options to those within an octave interval
1{playNote(MNRL, T) : MNRL > MNR-13, MNRL < MNR+13, melodyNoteRange(MNRL)}1 :- play(T), holdNote(MNR, T), melodyNoteRange(MNR), time(T).

% Don't jump more than a major fifth interval if a note was just played
1{playNote(MNRL, T) : MNRL > MNR-8, MNRL < MNR+8, melodyNoteRange(MNRL)}1 :- play(T), play(T-1), holdNote(MNR, T), melodyNoteRange(MNR), time(T).

% If a note is held for a while, make sure it's at least part of the chord being played
:- playNote(MNR, T-2), not play(T-1), not play(T), playMajorChord(CN, T/8), not majorChordNote(CN, MNR), melodyNoteRange(MNR), time(T).
:- playNote(MNR, T-2), not play(T-1), not play(T), playMinorChord(CN, T/8), not minorChordNote(CN, MNR), melodyNoteRange(MNR), time(T).

% If a note jumps more than a major third interval, it should jump to a note in the chord
:- playNote(MNR, T), holdNote(MNR1, T-1), |MNR-MNR1| > 4, playMajorChord(CN, T/8), not majorChordNote(CN, MNR), melodyNoteRange(MNR), melodyNoteRange(MNR1), time(T).
:- playNote(MNR, T), holdNote(MNR1, T-1), |MNR-MNR1| > 4, playMinorChord(CN, T/8), not minorChordNote(CN, MNR), melodyNoteRange(MNR), melodyNoteRange(MNR1), time(T).

% Don't hold notes not in the chord when the chord is played (it sounds bad)
% Note: This assumes a 4/4 time signature and will need to be generalized in the future
:- holdNote(MNR, T-1), not play(T), T \ 8 == 0, playMajorChord(CN, T/8), not majorChordNote(CN, MNR), melodyNoteRange(MNR), time(T).
:- holdNote(MNR, T-1), not play(T), T \ 8 == 0, playMinorChord(CN, T/8), not minorChordNote(CN, MNR), melodyNoteRange(MNR), time(T).
:- holdNote(MNR, T), T \ 8 == 0, playMajorChord(CN, T/8), not majorChordNote(CN, MNR), melodyNoteRange(MNR), time(T).
:- holdNote(MNR, T), T \ 8 == 0, playMinorChord(CN, T/8), not minorChordNote(CN, MNR), melodyNoteRange(MNR), time(T).

% Play only 1 note at a time
:- 2{playNote(MNRL, T) : melodyNoteRange(MNRL)}, time(T).

%*
#show playNote/2.
#show releaseNote/2.
*%